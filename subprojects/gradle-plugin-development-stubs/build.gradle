/*
 * Copyright 2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly(project(':gradle-api')) {
        attributes {
            attribute(Attribute.of('dev.gradleplugins.gradleAbi', String), '2.14.1')
        }
    }
}

configurations {
    stubElements {
        canBeResolved = false
        canBeConsumed = true
        attributes.attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, 'stub'))
        outgoing.artifact(project.layout.buildDirectory.dir('classes/java/main')) {
            builtBy(tasks.named('compileJava'))
        }
    }
}

tasks.named('compileJava', JavaCompile) {
    options.debugOptions.debugLevel = 'none'
}

import com.jfrog.bintray.gradle.tasks.BintrayUploadTask
import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact

import java.text.SimpleDateFormat

plugins {
    id 'com.jfrog.bintray'
    id 'maven-publish'
}
apply plugin: 'dev.gradleplugins.java-gradle-plugin'

java {
    withSourcesJar()
    withJavadocJar()
}

gradlePlugin {
    plugins {
        javaGradlePlugin {
            id = 'dev.gradleplugins.java-gradle-plugin'
            implementationClass = 'dev.gradleplugins.internal.plugins.JavaGradlePluginDevelopmentPlugin'
        }
        groovyGradlePlugin {
            id = 'dev.gradleplugins.groovy-gradle-plugin'
            implementationClass = 'dev.gradleplugins.internal.plugins.GroovyGradlePluginDevelopmentPlugin'
        }
    }
    extra {
        minimumGradleVersion = '6.2.1'
    }
}

repositories {
    jcenter()
    maven { url = 'https://dl.bintray.com/gradle-plugins/distributions' }
}

dependencies {
    compileOnly 'dev.gradleplugins:gradle-api:6.2.1'
    compileOnly 'org.codehaus.groovy:groovy:2.5.8'
    functionalTestImplementation(project(':gradle-fixtures')) {
        capabilities {
            requireCapability('dev.gradleplugins:gradle-fixtures-spock-support')
        }
    }
}

// Temporary workaround for https://github.com/bintray/gradle-bintray-plugin/issues/229
PublishingExtension publishing = project.extensions.getByType(PublishingExtension)
project.tasks.withType(BintrayUploadTask).configureEach {
    doFirst {
        publishing.publications.withType(MavenPublication).each { publication ->
            File moduleFile = project.buildDir.toPath()
                    .resolve("publications/${publication.name}/module.json").toFile()

            if (moduleFile.exists()) {
                publication.artifact(new FileBasedMavenArtifact(moduleFile) {
                    @Override
                    protected String getDefaultExtension() {
                        return "module"
                    }
                })
            }
        }
    }
}

private String resolveProperty(Project project, String envVarKey, String projectPropKey) {
    Object propValue = System.getenv().get(envVarKey);

    if (propValue != null) {
        return propValue.toString();
    }

    propValue = project.findProperty(projectPropKey);
    if (propValue != null) {
        return propValue.toString();
    }

    return null;
}


afterEvaluate {
    bintray {
        user = resolveProperty(project, "BINTRAY_USER", "dev.gradleplugins.bintray.user")
        key = resolveProperty(project, "BINTRAY_KEY", "dev.gradleplugins.bintray.key")
        publications = publishing.publications.collect { it.name }

        publish = true
        override = System.getProperties().containsKey('force')

        pkg {
            repo = 'distributions'
            name = 'dev.gradleplugins:gradle-development-plugins'
            desc = project.description
            userOrg = 'gradle-plugins'
            websiteUrl = 'https://nokee.dev'
            issueTrackerUrl = 'https://github.com/gradle-plugins/toolbox/issues'
            vcsUrl = 'https://github.com/gradle-plugins/toolbox.git'
            labels = ['gradle', 'gradle-api', 'gradle-plugins']
            licenses = ['Apache-2.0']
            publicDownloadNumbers = false

            version {
                released = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date())
                // TODO: Sign artifacts
                gpg {
                    sign = false
                    passphrase = resolveProperty(project, "GPG_PASSPHRASE", "dev.gradleplugins.bintray.gpgPassphrase")
                }
            }
        }
    }
}

tasks.register('release') {
    dependsOn('bintrayUpload')
}
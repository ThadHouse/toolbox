import com.jfrog.bintray.gradle.BintrayExtension

plugins {
    id 'java-library'
    id 'groovy' // for Spock framework
    id 'dev.gradleplugins.experimental.shaded-artifact'
    id 'dev.gradleplugins.experimental.artifacts'
    id 'dev.gradleplugins.experimental.publishing'
}

description = "Gradle TestKit fixtures for fast and efficient Gradle plugin development."

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
    gradlePluginPortal()
}

dependencies {
    // This is tricky as it's a API dependency but isn't published anywhere
    //   Let's put the burden on the user to declare that as well
    //   They should be declaring that dependency anyway as this library is not meant to be used outside of the Gradle plugin development plugins within Gradle runtime.
    //   TODO: This should probably be compileOnly
    implementation gradleTestKit()

    api 'net.rubygrapefruit:ansi-control-sequence-util:0.2'

    implementation 'org.apache.commons:commons-lang3:3.9' // For StringUtils
    implementation 'org.hamcrest:hamcrest:2.2'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.apache.ant:ant:1.9.13'
    implementation 'org.ow2.asm:asm:6.0'

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation project(':spock-test-directory-extension')
    implementation 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava {
            from(components.java)
        }
    }
}

afterEvaluate {
    extensions.getByType(BintrayExtension).with {
        pkg {
            name = "testkit-fixtures"
        }
    }
}

tasks.register("release") {
    dependsOn("bintrayUpload")
}

tasks.register("install") {
    dependsOn("publishToMavenLocal")
}

// TODO: fix all javadoc issues
